buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.13'
    }
}

plugins {
    id "com.google.protobuf" version "0.8.13"
    id "maven-publish"
    id "java"
}

group = 'com.exactpro.grpc'
version = "1.0-SNAPSHOT"

sourceCompatibility = 1.11
targetCompatibility = 1.11

ext {
    grpcVersion         = '1.32.1'
    protobufVersion     = '3.13.0'

    genDir              = file('src/gen')
    sharedDir           = file("${project.rootDir}/shared")
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "io.grpc:grpc-core:${grpcVersion}"
    compile "io.grpc:grpc-netty:${grpcVersion}"

    compile "javax.annotation:javax.annotation-api:1.3.2"
}

sourceSets {
    main {
        proto {
            srcDirs 'src/proto'
        }

        java {
            srcDirs 'src/gen/main/java', 'src/gen/main/grpc'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks.generatedFilesBaseDir = genDir
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
        ofSourceSet('main')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "localRepo"
            url = sharedDir
        }
    }
}

clean {
    delete genDir
    delete sharedDir
}